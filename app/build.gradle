apply plugin: 'com.android.application'
apply plugin: 'dagger.hilt.android.plugin'
apply plugin: "androidx.navigation.safeargs"

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.1"

    defaultConfig {
        applicationId "com.hava.trips"
        minSdkVersion 19
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true

        testInstrumentationRunner "com.hava.trips.utils.HavaTripsTestRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        dataBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        def srcDir = 'src/sharedTest/java'
        test {
            java.srcDirs += srcDir
        }
        androidTest {
            java.srcDirs += srcDir
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true // required to not break unit tests
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    def fragment_version = '1.2.5'
    def material_version = '1.3.0-alpha02'
    def appcompat_version = '1.2.0'
    def multidex_version = '2.0.1'
    def nav_version = '2.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation "com.google.android.material:material:$material_version"
    implementation "androidx.appcompat:appcompat:$appcompat_version"
    implementation "androidx.multidex:multidex:$multidex_version"

    // Hilt-DI
    def hilt_version = '2.28.3-alpha'
    def hilt_jetpack_version = '1.0.0-alpha02'
    implementation "com.google.dagger:hilt-android:$hilt_version"
    implementation "androidx.hilt:hilt-lifecycle-viewmodel:$hilt_jetpack_version"
    annotationProcessor "com.google.dagger:hilt-android-compiler:$hilt_version"
    annotationProcessor "androidx.hilt:hilt-compiler:$hilt_jetpack_version"

    // Room
    def room_version = "2.2.5"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-rxjava2:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"

    // Navigation
    implementation "androidx.navigation:navigation-fragment:$nav_version"
    implementation "androidx.navigation:navigation-ui:$nav_version"

    // RxJava
    implementation 'io.reactivex.rxjava3:rxandroid:3.0.0'
    implementation 'io.reactivex.rxjava3:rxjava:3.0.4'

    // Square and Retrofit
    def retrofit_version = '2.9.0'
    def logging_interceptor_version = '4.8.1'
    def okio_version = '2.7.0'
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.9.0'
    implementation "com.squareup.okhttp3:logging-interceptor:$logging_interceptor_version"
    implementation "com.squareup.okio:okio:$okio_version"

    // Others
    implementation 'com.google.android.gms:play-services-maps:17.0.0'
    implementation "com.github.bumptech.glide:glide:4.11.0"

    // Local(Unit) tests
    def espresso_version = '3.3.0-rc03'
    def junit_version = '4.13'
    def android_junit_version = '1.1.1'
    def arch_core_testing_version = '2.1.0'
    def robolectric_version = '4.3.1'
    def mockito_core_version = '3.3.3'
    def mockito_dexmaker_version = '2.12.1'
    def hamcrest_version = '1.3'
    def android_test_rules_version = '1.3.0-rc03'
    def android_core_ktx_version = '1.2.0'
    def androidx_annotation_version = '1.1.0'
    testImplementation "junit:junit:$junit_version"
    testImplementation "org.hamcrest:hamcrest-all:$hamcrest_version"
    testImplementation "androidx.arch.core:core-testing:$arch_core_testing_version"
    testImplementation "org.robolectric:robolectric:$robolectric_version"
    testImplementation "androidx.test:core-ktx:$android_core_ktx_version"
    testImplementation "androidx.test.ext:junit:$android_junit_version"
    testImplementation "androidx.room:room-testing:$room_version"
    testImplementation "com.google.dagger:hilt-android-testing:$hilt_version"
    testAnnotationProcessor "com.google.dagger:hilt-android-compiler:$hilt_version"

    // Instrumented tests.
    implementation "androidx.test.espresso:espresso-idling-resource:$espresso_version"
    androidTestImplementation "androidx.navigation:navigation-testing:$nav_version"
    androidTestImplementation "androidx.annotation:annotation:$androidx_annotation_version"
    androidTestImplementation "androidx.test:rules:$android_test_rules_version"
    androidTestImplementation "org.mockito:mockito-core:$mockito_core_version"
    androidTestImplementation "com.linkedin.dexmaker:dexmaker-mockito:$mockito_dexmaker_version"
    androidTestImplementation "androidx.test.ext:junit:$android_junit_version"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_version"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$espresso_version"
    androidTestImplementation "com.google.dagger:hilt-android-testing:$hilt_version"
    androidTestAnnotationProcessor "com.google.dagger:hilt-android-compiler:$hilt_version"
    // Testing code should not be included in the main code.
    // Once https://issuetracker.google.com/128612536 is fixed this can be fixed.
    debugImplementation("androidx.fragment:fragment-testing:$fragment_version") {
        exclude group: 'androidx.test', module: 'core'
    }

}